apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: "com.github.spotbugs"
apply plugin: "org.sonarqube"

checkstyle {
    toolVersion "${checkstyle_plugin_version}"
    maxWarnings = 0
    configFile = file("build-config/checkstyle/checkstyle.xml")
    sourceSets = [sourceSets.main]
}

pmd {
    toolVersion = "${pmd_plugin_version}"
    consoleOutput = true
    ruleSets = []
    ruleSetFiles = files("build-config/pmd/pmd-ruleset.xml")
    sourceSets = [sourceSets.main]
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

jacoco {
    toolVersion = "${jacoco_plugin_version}"
}

def jacocoExcludes = [
    '**/*Application.*',
    '**/config/**',
    '**/models/**',
    '**/exceptions/**',
    '**/dtos/**',
    '**/*Constants*',
]

jacocoTestReport {
    executionData tasks.withType(Test)
    classDirectories.from = files(sourceSets.main.output.classesDirs)
    sourceDirectories.from = files(sourceSets.main.java.srcDirs)

    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        }))
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        }))
    }
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = new BigDecimal("${jacoco_min_coverage_required}")
            }
            excludes = jacocoExcludes
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

file("sonar-project.properties").withReader {
    Properties sonarProperties = new Properties()
    sonarProperties.load(it)

    sonarProperties.each { key, value ->
        sonarqube {
            properties {
                property key, value
            }
        }
    }
}
